import net.ltgt.gradle.errorprone.CheckSeverity

plugins {
  id 'org.springframework.boot' version '2.6.4'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'net.ltgt.errorprone' version '2.0.2'
  id 'com.diffplug.spotless' version '6.3.0'
  id 'com.github.spotbugs' version '5.0.6'
  id 'checkstyle'
  id 'java'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter'
  compileOnly 'org.projectlombok:lombok'
  compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
  developmentOnly 'org.springframework.boot:spring-boot-devtools'
  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
  annotationProcessor 'org.projectlombok:lombok'
  annotationProcessor 'com.uber.nullaway:nullaway:0.9.5'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  errorprone 'com.google.errorprone:error_prone_core:2.11.0'
}

checkstyle {
  configProperties = [
    'org.checkstyle.google.suppressionfilter.config': rootProject.file('config/checkstyle/suppressions.xml'),
    'org.checkstyle.google.suppressionxpathfilter.config': rootProject.file('config/checkstyle/suppressions-xpath.xml'),
  ]
  maxWarnings = 0
  toolVersion = '9.3'
}

spotless {
  if (System.env.CI != 'true') {
    ratchetFrom 'origin/main'
  }
  java {
    importOrder()
    removeUnusedImports()
    googleJavaFormat()
  }
}

tasks.withType(JavaCompile).configureEach {
  options.errorprone {
    option('NullAway:AnnotatedPackages', 'com.example')
  }
}

tasks.named('compileJava').configure {
  options.errorprone {
    error('NullAway')
  }
}

tasks.named('compileTestJava').configure {
  options.errorprone {
    disable('UnicodeInCode')
  }
}

tasks.named('test') {
  useJUnitPlatform()
}
